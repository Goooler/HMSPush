import com.android.build.gradle.BasePlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application' version '7.3.0' apply false
    id 'com.android.library' version '7.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '1.7.10' apply false
}

allprojects {
    plugins.withType(BasePlugin).configureEach {
        project.android {
            compileSdk 33
            defaultConfig {
                minSdk 26
                targetSdk 33
                vectorDrawables.useSupportLibrary = true
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }

        def versionName = stdout.toString().trim()
        if (versionName.startsWith("v")) {
            return versionName.substring(1)
        } else {
            return versionName
        }
    } catch (ignored) {
        return null;
    }
}
ext {
    gitVersionCode = getVersionCode()
    gitVersionName = getVersionName()
    APPLICATION_ID = "one.yufz.hmspush"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}