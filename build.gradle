import com.android.build.gradle.BasePlugin

plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.21' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '1.9.21' apply false
}

allprojects {
    plugins.withType(BasePlugin).configureEach {
        project.android {
            compileSdk 34
            defaultConfig {
                minSdk 26
                targetSdk 34
                vectorDrawables.useSupportLibrary = true
            }
            // Can remove this once https://issuetracker.google.com/issues/260059413 is fixed.
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }
    }

    // Configure Java to use our chosen language level. Kotlin will automatically pick this up.
    // See https://kotlinlang.org/docs/gradle-configure-project.html#gradle-java-toolchains-support
    plugins.withType(JavaBasePlugin).configureEach {
        java.toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }
}

final def getVersionCode = providers.exec {
    commandLine('git', 'rev-list', '--first-parent', '--count', 'HEAD')
}.standardOutput.asText.get().trim()

final def getVersionName = providers.exec {
    commandLine('git', 'describe', '--tags', '--dirty')
}.standardOutput.asText.get().trim() - "v"

ext {
    gitVersionCode = getVersionCode
    gitVersionName = getVersionName
    APPLICATION_ID = "one.yufz.hmspush"
}

tasks.register('clean') {
    delete rootProject.layout.buildDirectory
}
