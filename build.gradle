import com.android.build.gradle.BasePlugin

plugins {
    id 'com.android.application' version '7.4.0' apply false
    id 'com.android.library' version '7.4.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.0' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '1.8.0' apply false
}

allprojects {
    plugins.withType(BasePlugin).configureEach {
        project.android {
            compileSdk 33
            defaultConfig {
                minSdk 26
                targetSdk 33
                vectorDrawables.useSupportLibrary = true
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }

    // Configure Java to use our chosen language level. Kotlin will automatically pick this up.
    // See https://kotlinlang.org/docs/gradle-configure-project.html#gradle-java-toolchains-support
    plugins.withType(JavaBasePlugin).configureEach {
        java.toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }
}

final def getVersionCode = providers.exec {
    commandLine('git', 'rev-list', '--first-parent', '--count', 'HEAD')
}.standardOutput.asText.get().trim()

final def getVersionName = providers.exec {
    commandLine('git', 'describe', '--tags', '--dirty')
}.standardOutput.asText.get().trim() - "v"

ext {
    gitVersionCode = getVersionCode
    gitVersionName = getVersionName
    APPLICATION_ID = "one.yufz.hmspush"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
